import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id "build.buf" version "0.8.5"
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.jetbrains.kotlin.plugin.spring'
apply plugin: 'com.google.protobuf'

group = 'ru.neoflex.market'
version = '0.0.1-SNAPSHOT'

def grpcVersion = '1.57.2'
def grpcKotlinVersion = '1.4.1'
def protobufVersion = '3.24.1'

dependencies {
    implementation("net.devh:grpc-server-spring-boot-starter:2.15.0.RELEASE")
    implementation("io.grpc:grpc-netty:${grpcVersion}")
    implementation("io.grpc:grpc-kotlin-stub:${grpcKotlinVersion}")
    implementation("io.grpc:grpc-protobuf:${grpcVersion}")
    implementation("com.google.protobuf:protobuf-kotlin:${protobufVersion}")
}

buf {
    configFileLocation = rootProject.file('buf.yml')
    generate {
        configFileLocation = rootProject.file('buf.gen.yaml')
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
        grpckt { artifact = "io.grpc:protoc-gen-grpc-kotlin:${grpcKotlinVersion}:jdk8@jar" }
    }
    generateProtoTasks {
        all().forEach { task ->
            task.plugins {
                grpc {}
                grpckt {}
            }
            task.builtins {
                kotlin {}
            }
        }
    }
}

tasks.withType(KotlinCompile) {
    kotlinOptions {
        freeCompilerArgs += '-Xjsr305=strict'
        jvmTarget = '17'
    }
}

